
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import('env')

from SCons.Util import CLVar

info_sources = [
    'info/device_info.cpp',
    'info/device_query.cpp',
    'info/platform_info.cpp',
    'info/platform_layer_info.cpp',
    'info/platform_query.cpp',
]

info_headers = [
    'info/device_info.hpp',
    'info/device_info_ptr.hpp',
    'info/device_info_ptrs.hpp',
    'info/device_query.hpp',
    'info/platform_info.hpp',
    'info/platform_info_ptr.hpp',
    'info/platform_info_ptrs.hpp',
    'info/platform_layer_info.hpp',
    'info/platform_query.hpp',
]

env['CLXX_PACKAGES']['libclxx_info'] = {
    'version' : env.subst('$CLXX_VERSION'),
    'libdeps' : CLVar()
}

objects = env.SharedObject(info_sources)

overrides = {
    'SHLIBVERSION'  : env['CLXX_PACKAGES']['libclxx_info']['version'],
    'LIBS'          : env['CLXX_PACKAGES']['libclxx_info']['libdeps']
}

lib = env.SharedLibrary('../clxx_info', objects, **overrides)

ilib = env.InstallVersionedLib('${libdir}', lib, **overrides)
env.Ignore("${INSTALL_SANDBOX}${libdir}", ilib)

iheaders = env.Install("${includedir}/clxx/info", info_headers)
env.Ignore("${INSTALL_SANDBOX}${includedir}/clxx/info", iheaders)

env.Alias('install-libclxx_info-headers', iheaders)
env.Alias('install-libclxx_info-lib', ilib)
env.Alias('install-libclxx_info-dev', ['install-libclxx_info-headers', 'install-libclxx_info-lib' ])
env.Alias('install','install-libclxx_info-dev')

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
