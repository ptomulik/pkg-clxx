
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import('conf')

import re
import sys
import SCons.Util
from SConsGnu import CcChecks
from SConsGnu.Cc import CanonCC, CanonCXX

_ccWDeprecatedDeclarations = None;
_ccWNoDeprecatedDeclarations = None;
_cxxWDeprecatedDeclarations = None;
_cxxWNoDeprecatedDeclarations = None;
_ccWerror = None;
_cxxWerror = None;

def HasCcWflag(conf, flag):
    d = conf.env['CC_HAS_FLAG']
    d[flag] = conf.CheckCCFlag(flag, CCFLAGS=['-Werror'])
def HasCxxWflag(conf, flag):
    d = conf.env['CXX_HAS_FLAG']
    d[flag] = conf.CheckCXXFlag(flag, CXXFLAGS=['-Werror'])

def CheckClHVersion(ctx, ver):
    ctx.Message('Checking for OpenCL %s support in CL/cl.h... ' % ver)
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
#if CL_VERSION_%s_%s
    return 0;
#else
    return 1;
#endif
}

""" % tuple(ver.split('.'))
    success, outstr = ctx.TryRun(prog,'.c')
    ctx.Result(success)
    return success

def TryLinkClFunction(ctx, fun, code):
    ctx.Message('Checking whether %s() exists in libOpenCL.so... ' % fun)
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
    %s
    return 0;
}

""" % code
    flags = { '-CCFLAGS'    : _ccWDeprecatedDeclarations,
              '-CFLAGS'     : _ccWDeprecatedDeclarations,
              'CCFLAGS'     : _ccWNoDeprecatedDeclarations }
    success = CcChecks.TryLinkWithFlags(ctx, flags, prog, '.c')
    ctx.Result(success)
    return success;

def TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s() compiles with %s' % (fun, flag))
        ctx.did_show_result = 1
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
    %s
    return 0;
}

""" % code
    success = CcChecks.TryCompileWithFlags(ctx, flags, prog, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CheckClFunctionDeclared(ctx, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s() is declared... ' % fun)
        ctx.did_show_result = 1
    flags = { 'CCFLAGS'     : _ccWNoDeprecatedDeclarations,
              '-CCFLAGS'    : _ccWDeprecatedDeclarations,
              '-CFLAGS'     : _ccWDeprecatedDeclarations }
    success = TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CheckClFunctionNotDeprecated(ctx, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s() is not deprecated... ' % fun)
        ctx.did_show_result = 1
    flags = { 'CCFLAGS'     : _ccWDeprecatedDeclarations + ' ' + _ccWerror,
              '-CCFLAGS'    : _ccWNoDeprecatedDeclarations,
              '-CFLAGS'     : _ccWNoDeprecatedDeclarations }
    success = TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CharacterizeClFunction(conf, fun, code):
    # Check if a function is declared in CL/cl.h
    if conf.CheckClFunctionDeclared(fun, code):
        conf.Define('CLXX_IS_DECLARED_%s' % fun, 1, 'OpenCL header declares %s()' % fun)
        # If declared, check whether it's not deprecated
        if not conf.CheckClFunctionNotDeprecated(fun, code):
            conf.Define('CLXX_IS_DEPRECATED_%s' % fun, 1, '%s is declared as deprecated' % fun)
        # ... and whether libOpenCL.so has it as well
        if conf.TryLinkClFunction(fun, code):
            conf.Define('CLXX_IS_LINKABLE_%s' % fun, 1, 'libOpenCL had the %s when libclxx was build' % fun)

conf.AddTests(CcChecks.Tests())
conf.AddTests({ 'CheckClHVersion' : CheckClHVersion })
conf.AddTests({ 'TryLinkClFunction' : TryLinkClFunction })
conf.AddTests({ 'TryCompileClFunctionWithFlags' : TryCompileClFunctionWithFlags })
conf.AddTests({ 'CheckClFunctionDeclared' : CheckClFunctionDeclared })
conf.AddTests({ 'CheckClFunctionNotDeprecated' : CheckClFunctionNotDeprecated })

conf.env['CC_HAS_FLAG'] = {}
conf.env['CXX_HAS_FLAG'] = {}

##############################################################################
# Version stuff
##############################################################################
ver_str = conf.env.subst("${CLXX_VERSION}")
ver_triple = map(lambda x : int(x), re.findall(r'([0-9]+)', ver_str)[0:3])
(ver_major, ver_minor, ver_patch) = ver_triple
ver_hex = (ver_major << 16) | (ver_minor << 8) | ver_patch;
conf.Define('CLXX_VERSION_STRING', '"%s"' % ver_str, "Library version as string")
conf.Define('CLXX_VERSION_MAJOR', ver_major, "Library version: major number")
conf.Define('CLXX_VERSION_MINOR', ver_minor, "Library version: minor number")
conf.Define('CLXX_VERSION_PATCH', ver_patch, "Library version: patch number")
conf.Define('CLXX_VERSION', "0x{0:06x}".format(ver_hex), "Library version: 0xMMmmpp (MM - major, mm - minor, pp - patch)")
##############################################################################

if sys.maxsize > 2**32:
    conf.Define( 'CLXX_ARCH_64BIT', None, "Library was built for 64-bit host architecture" )

cc = CanonCC(conf.env)
if cc == 'clang':
    HasCcWflag(conf, '-Wdeprecated-register')
    HasCcWflag(conf, '-Wdeprecated-declarations')
    HasCcWflag(conf, '-Wunused-parameter')
    _ccWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _ccWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _ccWerror = '-Werror'
elif cc == 'gcc':
    HasCcWflag(conf, '-Wmaybe-uninitialized')
    HasCcWflag(conf, '-Wdeprecated-declarations')
    _ccWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _ccWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _ccWerror = '-Werror'

cxx = CanonCXX(conf.env)
if cxx == 'clang++':
    HasCxxWflag(conf, '-Wdeprecated-register')
    HasCxxWflag(conf, '-Wdeprecated-declarations')
    HasCxxWflag(conf, '-Wunused-parameter')
    _cxxWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _cxxWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _cxxWerror = '-Werror'
elif cxx == 'g++':
    HasCxxWflag(conf, '-Wmaybe-uninitialized')
    HasCxxWflag(conf, '-Wdeprecated-declarations')
    _cxxWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _cxxWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _cxxWerror = '-Werror'

# Prepare modified environment for the OpenCL checks
orgEnv = conf.env
conf.env = orgEnv.Clone()
conf.env.AppendUnique(LIBS = ['OpenCL'])

if conf.CheckClHVersion('1.0'):
    conf.Define( 'CLXX_CL_H_VERSION_1_0', 1, 'Library was built against CL/cl.h which had OpenCL 1.0 features')
if conf.CheckClHVersion('1.1'):
    conf.Define( 'CLXX_CL_H_VERSION_1_1', 1, 'Library was built against CL/cl.h which had OpenCL 1.1 features')
if conf.CheckClHVersion('1.2'):
    conf.Define( 'CLXX_CL_H_VERSION_1_2', 1, 'Library was built against CL/cl.h which had OpenCL 1.2 features')
if conf.CheckClHVersion('2.0'):
    conf.Define( 'CLXX_CL_H_VERSION_2_0', 1, 'Library was built against CL/cl.h which had OpenCL 2.0 features')

#
# Checking for the existence/feasibility of certain OpenCL functions.
#

#
# clCreateSubDevices()
#
_clCreateSubDevices = \
"""clCreateSubDevices(
        (cl_device_id)NULL,
        (const cl_device_partition_property*)NULL,
        0u,
        (cl_device_id*)NULL,
        (cl_uint*)NULL);
"""
CharacterizeClFunction(conf, 'clCreateSubDevices', _clCreateSubDevices)

#
# clRetainDevice()
#
_clRetainDevice = \
"""clRetainDevice((cl_device_id)NULL);
"""
CharacterizeClFunction(conf, 'clRetainDevice', _clRetainDevice)

#
# clReleaseDevice()
#
_clReleaseDevice = \
"""clReleaseDevice((cl_device_id)NULL);
"""
CharacterizeClFunction(conf, 'clReleaseDevice', _clReleaseDevice)

#
# clCreateProgramWithBuiltInKernels()
#
_clCreateProgramWithBuiltInKernels = \
"""clCreateProgramWithBuiltInKernels(
        (cl_context)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        (cl_int*)NULL);
"""
CharacterizeClFunction(conf, 'clCreateProgramWithBuiltInKernels', _clCreateProgramWithBuiltInKernels)

#
# clCompileProgram()
#
_clCompileProgram = \
"""clCompileProgram(
        (cl_program)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        0,
        (const cl_program*)NULL,
        (const char**)NULL,
        (void(CL_CALLBACK*)(cl_program,void*))NULL,
        NULL);
"""
CharacterizeClFunction(conf, 'clCompileProgram', _clCompileProgram)

#
# clLinkProgram()
#
_clLinkProgram = \
"""clLinkProgram(
        (cl_context)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        0,
        (const cl_program*)NULL,
        (void(CL_CALLBACK*)(cl_program,void*))NULL,
        NULL,
        NULL);
"""
CharacterizeClFunction(conf, 'clLinkProgram', _clLinkProgram)

#
# clUnloadPlatformCompiler()
#
_clUnloadPlatformCompiler = \
"""clUnloadPlatformCompiler((cl_platform_id)NULL);
"""
CharacterizeClFunction(conf, 'clUnloadPlatformCompiler', _clUnloadPlatformCompiler)

#
# clCreateCommandQueue()
#
# (removed/deprecated in OpenCL 2.0)
_clCreateCommandQueue = """
clCreateCommandQueue( (cl_context)0,
                      (cl_device_id)0,
                      (cl_command_queue_properties)0,
                      (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateCommandQueue', _clCreateCommandQueue)

#
# clCreateCommandQueueWithProperties()
#
# (added in OpenCL 2.0)
_clCreateCommandQueueWithProperties = \
"""clCreateCommandQueueWithProperties(  (cl_context)0,
                                        (cl_device_id)0,
                                        (cl_queue_properties const*)NULL,
                                        (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateCommandQueueWithProperties', _clCreateCommandQueueWithProperties)

#
# clCreateSubBuffer()
#
# (added in OpenCL 1.1)
_clCreateSubBuffer = \
"""clCreateSubBuffer( (cl_mem)0,
                      (cl_mem_flags)0,
                      (cl_buffer_create_type)0,
                      (const void*)NULL,
                      (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateSubBuffer', _clCreateSubBuffer)

#
# clEnqueueReadBufferRect()
#
# (added in OpenCL 1.1)
_clEnqueueReadBufferRect = \
"""clEnqueueReadBufferRect( (cl_command_queue)0,
                            (cl_mem)0,
                            (cl_bool)0,
                            (const size_t*)NULL,
                            (const size_t*)NULL,
                            (const size_t*)NULL,
                             0ul,
                             0ul,
                             0ul,
                             0ul,
                            (void*)NULL,
                             0u,
                            (const cl_event*)NULL,
                            (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueReadBufferRect', _clEnqueueReadBufferRect)

#
# clEnqueueWriteBufferRect()
#
# (added in OpenCL 1.1)
_clEnqueueWriteBufferRect = \
"""clEnqueueWriteBufferRect( (cl_command_queue)0,
                             (cl_mem)0,
                             (cl_bool)0,
                             (const size_t*)NULL,
                             (const size_t*)NULL,
                             (const size_t*)NULL,
                              0ul,
                              0ul,
                              0ul,
                              0ul,
                             (void*)NULL,
                              0u,
                             (const cl_event*)NULL,
                             (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueWriteBufferRect', _clEnqueueWriteBufferRect)

#
# clEnqueueCopyBufferRect()
#
# (added in OpenCL 1.1)
_clEnqueueCopyBufferRect = \
"""clEnqueueCopyBufferRect( (cl_command_queue)0,
                            (cl_mem)0,
                            (cl_mem)0,
                            (const size_t*)NULL,
                            (const size_t*)NULL,
                            (const size_t*)NULL,
                             0ul,
                             0ul,
                             0ul,
                             0ul,
                             0u,
                            (const cl_event*)NULL,
                            (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueCopyBufferRect', _clEnqueueCopyBufferRect)

#
# clEnqueueFillBuffer()
#
# (added in OpenCL 1.2)
_clEnqueueFillBuffer = \
"""clEnqueueFillBuffer( (cl_command_queue)0,
                        (cl_mem)0,
                        (const void*)NULL,
                         0ul,
                         0ul,
                         0ul,
                         0u,
                        (const cl_event*)NULL,
                        (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueFillBuffer', _clEnqueueFillBuffer)

#
# clCreateImage()
#
# (added in OpenCL 1.2)
_clCreateImage = \
"""clCreateImage( (cl_context)0,
                  (cl_mem_flags)0,
                  (const cl_image_format*)NULL,
                  (const cl_image_desc*)NULL,
                  (void*)NULL,
                  (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateImage', _clCreateImage)

#
# clEnqueueFillImage()
#
# (added in OpenCL 1.2)
_clEnqueueFillImage = \
"""clEnqueueFillImage( (cl_command_queue)0,
                        (cl_mem)0,
                        (const void*)NULL,
                        (const size_t*)NULL,
                        (const size_t*)NULL,
                         0u,
                        (const cl_event*)NULL,
                        (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueFillImage', _clEnqueueFillImage)

#
# clEnqueueMigrateMemObjects()
#
# (added in OpenCL 1.2)
_clEnqueueMigrateMemObjects = \
"""clEnqueueMigrateMemObjects( (cl_command_queue)0,
                                0u,
                               (const cl_mem*)NULL,
                               (cl_mem_migration_flags)0,
                                0u,
                               (const cl_event*)NULL,
                               (cl_event*)NULL );
"""
CharacterizeClFunction(conf, 'clEnqueueMigrateMemObjects', _clEnqueueMigrateMemObjects)

#
# clSetMemObjectDestructorCallback()
#
# (added in OpenCL 1.2)
_clSetMemObjectDestructorCallback = \
"""clSetMemObjectDestructorCallback( (cl_mem)0,
                                     (void(*)(cl_mem, void*))NULL,
                                     NULL );
"""
CharacterizeClFunction(conf, 'clSetMemObjectDestructorCallback', _clSetMemObjectDestructorCallback)

#
# clGetKernelArgInfo()
#
# (added in OpenCL 1.2)
_clGetKernelArgInfo = \
"""clGetKernelArgInfo(  (cl_kernel)0,
                        0,
                        (cl_kernel_arg_info)0,
                        0ul,
                        (void*)NULL,
                        (size_t*)NULL);
"""
CharacterizeClFunction(conf, 'clGetKernelArgInfo', _clGetKernelArgInfo)

#
# clSetKernelArgSVMPointer()
#
# (added in OpenCL 2.0)
_clSetKernelArgSVMPointer = \
"""clSetKernelArgSVMPointer( (cl_kernel)0,
                              0,
                             (const void*)NULL );
"""
CharacterizeClFunction(conf, 'clSetKernelArgSVMPointer', _clSetKernelArgSVMPointer)

#
# clSetKernelExecInfo()
#
# (added in OpenCL 2.0)
_clSetKernelExecInfo = \
"""clSetKernelExecInfo( (cl_kernel)0,
                        (cl_kernel_exec_info)0,
                        0ul,
                        (const void*)NULL );
"""
CharacterizeClFunction(conf, 'clSetKernelExecInfo', _clSetKernelExecInfo)

#
# clCreateUserEvent()
#
# (added in OpenCL 1.1)
_clCreateUserEvent = \
"""clCreateUserEvent((cl_context)0, NULL);
"""
CharacterizeClFunction(conf, 'clCreateUserEvent', _clCreateUserEvent)

#
# clSetUserEventStatus()
#
# (added in OpenCL 1.1)
_clSetUserEventStatus = \
"""clSetUserEventStatus((cl_event)0, 0);
"""
CharacterizeClFunction(conf, 'clSetUserEventStatus', _clSetUserEventStatus)

#
# clSetEventCallback()
#
# (added in OpenCL 1.1)
_clSetEventCallback = \
"""clSetEventCallback((cl_event)0, 0, NULL, NULL);
"""
CharacterizeClFunction(conf, 'clSetEventCallback', _clSetEventCallback)

# Restore the original environment
conf.env = orgEnv

# --without-cl-egl
try:
    if conf.env['GVARS']['WITHOUT_CL_EGL']:
        conf.Define('CLXX_WITHOUT_CL_EGL', 1, 'Do not inclue CL/cl_egl.h')
except KeyError:
    pass

## # FIXME: for MAC-OS the header is OpenCL/cl.h
## conf.req_headers.append(('CL/cl.h','<>','C'))
## conf.req_libs.append({ 'library' : ['OpenCL'],
##                        'autoadd' : 0,
##                        'header'  : '#include <CL/cl.h>' })

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
